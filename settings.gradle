pluginManagement {
    apply from: file('gradle/dependencies.gradle')
    resolutionStrategy {
        eachPlugin {
            switch (requested.id.id) {
                case 'org.jetbrains.dokka':
                    useModule("org.jetbrains.dokka:dokka-gradle-plugin:${deps.versions.dokka}")
                    break
                case 'org.jetbrains.dokka-android':
                    useModule("org.jetbrains.dokka:dokka-android-gradle-plugin:${deps.versions.dokka}")
                    break
                case 'com.android.application':
                    useModule(deps.build.gradlePlugins.android)
                    break
                case 'com.android.library':
                    useModule(deps.build.gradlePlugins.android)
                    break
                case 'org.jetbrains.kotlin.jvm':
                    useModule(deps.build.gradlePlugins.kotlin)
                    break
                case 'org.jetbrains.kotlin.android':
                    useModule(deps.build.gradlePlugins.kotlin)
                    break
            }
        }
    }
    repositories {
        jcenter()
        google()
        gradlePluginPortal()
    }
}

rootProject.name = 'motif'
include 'lib'
include 'compiler'
include 'samples:sample'
include 'samples:sample-lib'
include 'samples:dagger-comparison'
include 'plugin'
include 'models'
include 'it'
include 'stub-compiler'
