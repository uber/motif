import org.jetbrains.dokka.gradle.DokkaTaskPartial

// https://github.com/gradle/gradle/issues/4848
gradle.startParameter.configureOnDemand = false


buildscript {
    apply from: 'gradle/dependencies.gradle'
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath deps.build.gradlePlugins.android
        classpath deps.build.gradlePlugins.intellij
        classpath deps.build.gradlePlugins.kotlin
        classpath deps.build.gradlePlugins.ksp
        classpath deps.build.gradlePlugins.dokka
        classpath deps.build.gradlePlugins.mavenPublish
        classpath deps.build.gradlePlugins.spotless
        classpath deps.build.gradlePlugins.shadow
        classpath deps.apacheClient
    }
}

subprojects {
    apply from: rootProject.file('gradle/dependencies.gradle')

    repositories {
        google()
        mavenCentral()
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency {
            if (requested.name.startsWith("kotlin-stdlib")) {
                useTarget("${requested.group}:${requested.name.replace("jre", "jdk")}:${requested.version}")
            } else if (requested.group == "org.jetbrains.kotlin") {
                useVersion(deps.versions.kotlin)
            } else if (requested.group == "com.google.auto" && requested.name == "auto-common") {
                useTarget(deps.autoCommon)
            } else if (requested.group == "org.jetbrains.kotlinx" && requested.name == "kotlinx-metadata") {
                useTarget(deps.kotlinxMetadata)
            } else if (requested.group == "com.google.devtools.ksp") {
                useVersion(deps.versions.ksp)
            } else if (requested.group == "androidx.room") {
                if (requested.name != "room-compiler-processing" && requested.name != "room-compiler-processing-testing") {
                    useVersion(deps.versions.room)
                }
            }
        }
    }

    afterEvaluate {
        def extraKotlincArgs = [
            "-Xjvm-default=all",
            "-opt-in=kotlin.RequiresOptIn",
            "-opt-in=kotlin.contracts.ExperimentalContracts",
        ]
        if (project.path.startsWith(":compiler")) {
            extraKotlincArgs += [
                "-opt-in=androidx.room.compiler.processing.ExperimentalProcessingApi"
            ]
        }
        boolean isKotlinLibrary = project.plugins.hasPlugin("org.jetbrains.kotlin.jvm") || project.plugins.hasPlugin("org.jetbrains.kotlin.android")
        if (isKotlinLibrary) {
            java {
                toolchain.languageVersion.set(JavaLanguageVersion.of(11))
            }
            kotlin {
                jvmToolchain(11)
            }
            it.tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
                kotlinOptions {
                    freeCompilerArgs += extraKotlincArgs
                }
            }
        }

        boolean isAndroidLibraryOrApp = project.plugins.hasPlugin("org.jetbrains.kotlin.android")
        if (isAndroidLibraryOrApp) {
            android {
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }

            }
        }

        if (isKotlinLibrary && isAndroidLibraryOrApp) {
            android {
                kotlinOptions {
                    jvmTarget = "1.8"
                }
            }
        }
    }

    if (project.path != ":tests") {
        apply plugin: 'com.diffplug.spotless'
        spotless {
            format 'misc', {
                target '*.md', '.gitignore'

                trimTrailingWhitespace()
                endWithNewline()
            }
            kotlin {
                target "src/**/*.kt"
                ktlint(deps.versions.ktlint).editorConfigOverride([
                        "indent_size": "2",
                        "continuation_indent_size": "4"
                ])
                ktfmt(deps.versions.ktfmt)
                licenseHeaderFile rootProject.file('config/spotless/copyright.kt')
                trimTrailingWhitespace()
                endWithNewline()
            }
            java {
                target "src/**/*.java"
                googleJavaFormat(deps.versions.gjf)
                licenseHeaderFile rootProject.file('config/spotless/copyright.java')
                removeUnusedImports()
                trimTrailingWhitespace()
                endWithNewline()
            }
            groovyGradle {
                target '*.gradle'
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    apply plugin: 'org.jetbrains.dokka'
    if (!project.name.contains("samples") && !project.name.contains("tests")) {
        tasks.withType(DokkaTaskPartial).configureEach {
            outputDirectory.set(new File(buildDir, "docs/partial"))
            moduleName.set(project.property("POM_ARTIFACT_ID").toString())
            moduleVersion.set(project.property("VERSION_NAME").toString())
        }
    }
}
